unit TestmsSerializeController;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit 
  being tested.

}

interface

uses
  TestFramework,
  msSerializeController,
  Data.DBXJSONReflect,
  JSON,
  FMX.Objects,
  msDiagramm,
  msShape
  ;

type
  TmsDiagrammCheck = reference to procedure (aDiagramm : TmsDiagramm);

  TestTmsSerializeController = class abstract(TTestCase)
  protected
    function ShapeClass: RmsShape; virtual; abstract;
    procedure SaveDiagrammAndCheck(aDiagramm: TmsDiagramm);
    procedure CreateDiagrammWithShapeAndSaveAndCheck(aShapeClass: RmsShape);
    procedure DeserializeDiargammAndCheck(aCheck: TmsDiagrammCheck);
  published
    procedure TestSerialize;
    procedure TestDeSerialize;
    procedure TestDeSerializeViaShapeCheck;
  end;//TestTmsSerializeController

  TestSerializeTriangle = class(TestTmsSerializeController)
  protected
    function ShapeClass: RmsShape; override;
  end;//TestSerializeTriangle

  TestSerializeRectangle = class(TestTmsSerializeController)
  protected
    function ShapeClass: RmsShape; override;
  end;//TestSerializeRectangle

  TestSerializeCircle = class(TestTmsSerializeController)
  protected
    function ShapeClass: RmsShape; override;
  end;//TestSerializeCircle

implementation

 uses
  System.SysUtils,
  msTriangle,
  msRectangle,
  msCircle,
  System.Types,
  System.Classes
  ;

function TestSerializeTriangle.ShapeClass: RmsShape;
begin
 Result := TmsTriangle;
end;

function TestSerializeRectangle.ShapeClass: RmsShape;
begin
 Result := TmsRectangle;
end;

function TestSerializeCircle.ShapeClass: RmsShape;
begin
 Result := TmsCircle;
end;

 const
  c_DiagramName = 'First Diagram';

procedure TestTmsSerializeController.SaveDiagrammAndCheck(aDiagramm: TmsDiagramm);
var
 l_FileSerialized, l_FileEtalon: TStringList;
 l_FileNameTest : String;
 l_FileNameEtalon : String;
begin
 l_FileNameTest := ClassName + '_'+ Name + '.json';
 l_FileNameEtalon := l_FileNameTest + '.etalon.json';
 TmsSerializeController.Serialize(l_FileNameTest, aDiagramm);
  // TODO: Validate method results
 l_FileSerialized := TStringList.Create;
 l_FileSerialized.LoadFromFile(l_FileNameTest);

 l_FileEtalon := TStringList.Create;
 l_FileEtalon.LoadFromFile(l_FileNameEtalon);

 CheckTrue(l_FileEtalon.Equals(l_FileSerialized));

 FreeAndNil(l_FileSerialized);
 FreeAndNil(l_FileEtalon);
end;

procedure TestTmsSerializeController.CreateDiagrammWithShapeAndSaveAndCheck(aShapeClass: RmsShape);
var
 l_Diagramm: TmsDiagramm;
 l_Image: TImage;
begin
 l_Image:= TImage.Create(nil);
 try
  l_Diagramm := TmsDiagramm.Create(l_Image, c_DiagramName);
  try
   l_Diagramm.ShapeList.Add(aShapeClass.Create(TmsMakeShapeContext.Create(TPointF.Create(10, 10),nil)));
   SaveDiagrammAndCheck(l_Diagramm);
  finally
   FreeAndNil(l_Image);
  end;
 finally
  FreeAndNil(l_Diagramm);
 end;//try..finally
end;

procedure TestTmsSerializeController.TestSerialize;
begin
 CreateDiagrammWithShapeAndSaveAndCheck(ShapeClass);
end;

procedure TestTmsSerializeController.DeserializeDiargammAndCheck(aCheck: TmsDiagrammCheck);
var
  l_Diagramm : TmsDiagramm;
  l_FileNameTest: string;
begin
 l_FileNameTest := ClassName + '_'+ 'TestSerialize' + '.json';
 l_Diagramm := TmsSerializeController.DeSerialize(l_FileNameTest);
 try
  aCheck(l_Diagramm);
 finally
  FreeAndNil(l_Diagramm);
 end;//try..finally
end;

procedure TestTmsSerializeController.TestDeSerialize;
begin
 DeserializeDiargammAndCheck(
  procedure (aDiagramm: TmsDiagramm)
  begin
   SaveDiagrammAndCheck(aDiagramm);
  end
 );
end;

procedure TestTmsSerializeController.TestDeSerializeViaShapeCheck;
var
  l_Diagramm : TmsDiagramm;
  l_FileNameTest: string;
begin
 l_FileNameTest := ClassName + '_'+ 'TestSerialize' + '.json';
 l_Diagramm := TmsSerializeController.DeSerialize(l_FileNameTest);
 try
  Check(l_Diagramm.ShapeList <> nil);
  Check(l_Diagramm.ShapeList.Count = 1);
  Check(l_Diagramm.ShapeList[0].HackInstance.ClassType = ShapeClass);
 finally
  FreeAndNil(l_Diagramm);
 end;//try..finally
end;

initialization
  // Register any test cases with the test runner
  RegisterTest(TestSerializeTriangle.Suite);
  RegisterTest(TestSerializeRectangle.Suite);
  RegisterTest(TestSerializeCircle.Suite);
end.

