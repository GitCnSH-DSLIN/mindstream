UNIT ElementsRTTI.ms.dict

USES 
 types.ms.dict
 axiom_push.ms.dict
 core.ms.dict
 NoStrangeSymbols.ms.dict
 WordsRTTI.ms.dict
;

USES
 CompileTimeVar.ms.dict
;

ARRAY CompileTime-VAR [empty] []
%REMARK 'Пустой итератор'

STRING ENGINE_WORD INTERFACE TYPE ModelElement
 %REMARK 'Элемент модели'
ENGINE_WORD TYPE STEREOTYPE
 %REMARK 'Стереотип элемента модели'
 
CONST kind_NormalClass 0
CONST kind_ParameterizedClass 1

CONST opkind_Normal 0
CONST opkind_Implemented 1
CONST opkind_Overridden 2
 
CONST class_Class 0
CONST class_Category 1
CONST class_Attribute 2
CONST class_Operation 3
CONST class_Dependency 4
CONST class_Parameter 5

CONST UnknownAccess -1 
CONST PrivateAccess 0
CONST ProtectedAccess 1
CONST PublicAccess 2
CONST ImplementationAccess 3

CONST lt_unknown -1
CONST lt_agr 0
CONST lt_lnk 1
CONST lt_ref 2

CONST at_unknown -1
CONST at_regular 0
CONST at_virtual 0
CONST at_abstract 1
CONST at_final 2
CONST at_override 3
 
BOOLEAN FUNCTION .IsWord
 IN aValue
 ( aValue IsObj ) AND ( aValue IS class::TtfwWord ) &gt;&gt;&gt; Result
; // .IsWord

BOOLEAN FUNCTION .IsSequence
 IN aValue
 ( aValue .IsWord ) AND ( aValue IS class::TkwBeginLikeCompiledCode ) &gt;&gt;&gt; Result
; // .IsSequence

USES
 ref.ms.dict
;

EXPORTS
 ref.ms.dict

MACRO elem_func
 Literal IN aName
 %SUMMARY 'Функция на элементе модели' ;
 aName |N this.method.addr nil 'ModelElement' do_word_func
; // elem_func

MACRO elem_func_v
 Literal IN aStereoName
 Literal IN aName
 %SUMMARY 'Виртуальная функция на элементе модели' ;
  %TODO 'Вообще говоря это потом надо скрестить с elem_func анализируя VIRTUAL, ABSTRACT и OVERRIDE'
 aName |N this.method.addr nil 'ModelElement' do_word_func
; // elem_func_v

BOOLEAN elem_func True
 true &gt;&gt;&gt; Result
; // True

PROCEDURE do_elem_proc_prim
 STRING IN aName
 ENGINE_WORD IN aSelf
 ENGINE_WORD IN aModifier
 STRING IN aType
 %SUMMARY 'Реализация elem_proc и elem_generator' ;
 Ctx:ClearTypeInfo
 if ( aType IsNil ! ) then
 begin
  aType Ctx:Parser:PushSymbol
 end // ( aType IsNil ! )
 aName aSelf aModifier 'ModelElement' do_word_func
; // do_elem_proc_prim

PROCEDURE do_elem_proc
 STRING IN aName
 ENGINE_WORD IN aSelf
 ENGINE_WORD IN aModifier
 %SUMMARY 'Реализация elem_proc и elem_generator' ;
 aName aSelf aModifier 'VOID' do_elem_proc_prim
; // do_elem_proc

MACRO elem:
 Literal IN aName
 %SUMMARY 'Процедура на элементе модели' ;
 aName |N this.method.addr nil '' do_elem_proc_prim
; // elem:

MACRO elem_proc
 Literal IN aName
 %SUMMARY 'Процедура на элементе модели' ;
 aName |N this.method.addr nil do_elem_proc
; // elem_proc

MACRO elem_generator
 Literal IN aName
 %SUMMARY 'Генератор содержимого элемента' ;
 aName |N this.method.addr nil do_elem_proc
; // elem_generator

MACRO elem_ref_proc
 Literal IN aName
 %SUMMARY 'Процедура на элементе модели, который передаётся по ссылке' ;
 aName |N this.method.addr Addr LVALUE_MOD do_elem_proc
; // elem_ref_proc

MACRO elem_iterator
 [ 'ARRAY elem_func' ] Ctx:Parser:PushArray
 @SELF Ctx:SetWordProducerForCompiledClass
; // elem_iterator

ARRAY FUNCTION .SequenceCode.It
  TkwBeginLikeCompiledCode IN aCode
 %SUMMARY 'Преобразует список компилированных слов в итератор вызывающий каждое из этих слов' ;
 aCode CodeIterator .map&gt; DO &gt;&gt;&gt; Result
; // .SequenceCode.It

ARRAY FUNCTION Seq:
  ^ IN aCode
 %SUMMARY 'Преобразует список компилированных слов в итератор вызывающий каждое из этих слов' ;
 aCode CodeIterator .map&gt; DO &gt;&gt;&gt; Result
; // Seq:

USES
 axiom:WordBox
;

: DoCache
  ModelElement IN aCacheWhere
  STRING IN aKey
  IN aDefault
  ^ IN aLambda
  
 if ( aCacheWhere IsNil ) then
 begin
  aLambda DO
 end // aCacheWhere IsNil
 else
 if ( aCacheWhere IsString ) then
 begin
  aLambda DO
 end // aCacheWhere IsString
 else
 begin 
  ModelElement VAR l_CacheWhere
  if ( aCacheWhere IsIntf ) then
  begin
   aCacheWhere pop:WordBox:Boxed &gt;&gt;&gt; l_CacheWhere
  end // ( aCacheWhere IsIntf )
  else
  begin
   aCacheWhere &gt;&gt;&gt; l_CacheWhere
  end
  
  'cache:' aKey Cat &gt;&gt;&gt; aKey
  VAR l_FieldVar
  l_CacheWhere %% aKey &gt;&gt;&gt; l_FieldVar
 
  if ( l_FieldVar NotValid ) then    
  begin
   VAR l_NewVar
   aKey false l_CacheWhere pop:NewWordDefinitor:CheckVar &gt;&gt;&gt; l_NewVar
   @SELF l_NewVar pop:Word:SetProducer
   TRY
    aLambda DO
   EXCEPT
    [ 'Ошибка вызова: ' aKey ' на ' l_CacheWhere pop:Word:Name ] strings:Cat .
    aDefault
   END
   l_NewVar pop:Word:SetValue
   l_NewVar DO
  end
  else 
  begin
   l_FieldVar DO
  end
 end // aCacheWhere IsNil
; // DoCache

: CacheWord
  ModelElement IN aCacheWhere
  TtfwWord IN aWord
  ^ IN aLambda
 aCacheWhere aWord pop:Word:Name nil DoCache ( aLambda DO )
; // CacheWord

MACRO Cache
 axiom:PushSymbol @SELF
 axiom:PushSymbol CacheWord
; // Cache

MACRO CacheMethod
 'Self' Ctx:Parser:PushSymbol
 axiom:PushSymbol @SELF
 axiom:PushSymbol CacheWord
; // CacheMethod

ANY elem_func ElemMemberPrim
  STRING IN aMemberName
  IN aDefaultValue
  
 TtfwWord VAR l_FieldVar
 
 Self %% aMemberName &gt;&gt;&gt; l_FieldVar
 
 if ( l_FieldVar NotValid ) then
 begin
  Self %% 'Original' &gt;&gt;&gt; l_FieldVar
  if ( l_FieldVar NotValid ) then
  begin
   aDefaultValue &gt;&gt;&gt; Result
  end // l_FieldVar NotValid
  else
  begin
   TtfwWord VAR l_Original
   l_FieldVar DO &gt;&gt;&gt; l_Original
   if ( l_Original IsNil ) then
   begin
    aDefaultValue &gt;&gt;&gt; Result
   end // l_Original IsNil
   else
   begin
    l_Original aMemberName aDefaultValue call.me &gt;&gt;&gt; Result
   end // l_Original IsNil
  end // l_FieldVar NotValid
 end // l_FieldVar NotValid
 else
 begin
  l_FieldVar DO &gt;&gt;&gt; Result
 end // l_FieldVar NotValid
; // ElemMemberPrim

ANY elem_func ElemMember
  STRING IN aMemberName
  IN aDefaultValue
  
 RULES 
  ( Self IsNil )
   aDefaultValue
  ( Self IsString )
   aDefaultValue
  ( Self IsIntf ) 
   ( Self pop:WordBox:Boxed aMemberName aDefaultValue .ElemMemberPrim )
  DEFAULT
   ( Self aMemberName aDefaultValue .ElemMemberPrim )
 ; // RULES
 &gt;&gt;&gt; Result
; // ElemMember

elem_iterator ElemList
  STRING IN aMemberName
 Self aMemberName [empty] DoCache
 (
  Self aMemberName [empty] .ElemMember 
 )
 &gt;&gt;&gt; Result 
; // ElemList

STRING elem_func ElemString
  STRING IN aMemberName
 Self aMemberName '' DoCache
 (
  Self aMemberName '' .ElemMember 
 ) 
 &gt;&gt;&gt; Result 
; // ElemString

TtfwWord elem_func ElemWord
  STRING IN aMemberName
 Self aMemberName nil DoCache
 (
  Self aMemberName nil .ElemMember 
 ) 
 &gt;&gt;&gt; Result 
; // ElemWord

elem_iterator Children
 Self 'Children' .ElemList &gt;&gt;&gt; Result
; // Children

elem_iterator Constants
 Self 'Constants' .ElemList &gt;&gt;&gt; Result
; // Constants

elem_iterator Dependencies
 Self 'Dependencies' .ElemList &gt;&gt;&gt; Result
; // Dependencies

elem_iterator Injected
 Self 'Injected' .ElemList &gt;&gt;&gt; Result
; // Injected

elem_iterator Forwarded
 Self 'Forwarded' .ElemList &gt;&gt;&gt; Result
; // Forwarded

elem_iterator Attributes
 Self 'Attributes' .ElemList &gt;&gt;&gt; Result
; // Attributes

elem_iterator Parameters
 Self 'Parameters' .ElemList &gt;&gt;&gt; Result
; // Parameters

elem_iterator Operations
 Self 'Operations' .ElemList &gt;&gt;&gt; Result
; // Operations

elem_iterator Overridden
 Self 'Overridden' .ElemList &gt;&gt;&gt; Result
; // Overridden

elem_iterator Implemented
 Self 'Implemented' .ElemList &gt;&gt;&gt; Result
; // Implemented

elem_iterator Inherits
 Self 'Inherits' .ElemList &gt;&gt;&gt; Result
; // Inherits

elem_iterator Implements
 Self 'Implements' .ElemList &gt;&gt;&gt; Result
; // Implements

STRING elem_func Name
 if ( Self IsString ) then
 begin
  Self &gt;&gt;&gt; Result
 end // Self IsString
 else
 begin
  Self 'Name' .ElemString &gt;&gt;&gt; Result
 end // Self IsString
; // Name

STRING elem_func Documentation
 Self '%SUM' .ElemString &gt;&gt;&gt; Result
; // Documentation

TtfwWord elem_func Target
 Self 'Target' .ElemWord &gt;&gt;&gt; Result
; // Target

TtfwWord elem_func ValueType
 Self 'ValueType' .ElemWord &gt;&gt;&gt; Result
; // ValueType

ANY elem_func GetUP
  Literal IN aName
  
 Self aName |N '' .ElemMember &gt;&gt;&gt; Result 
; // GetUP

BOOLEAN elem_func UPisTrue
  Literal IN aName
  
 Self aName |N false .ElemMember true ?== &gt;&gt;&gt; Result  
; // UPisTrue

TtfwWord STRING elem_func Stereotype
 Self 'Stereotype' .ElemWord &gt;&gt;&gt; Result
; // Stereotype

BOOLEAN elem_func IsStereotype
  ^ IN aStereo
  
 BOOLEAN FUNCTION IsStereoKindOf
   TtfwWord IN anOurStereo
   TtfwWord IN anOtherStereo
  anOurStereo 
  'isA:' anOtherStereo pop:Word:Name Cat 
  false
  DoCache (
   BOOLEAN VAR l_Is
   anOurStereo anOtherStereo == &gt;&gt;&gt; l_Is
   if ( l_Is ! ) then
   begin
    anOurStereo .Name anOtherStereo .Name == &gt;&gt;&gt; l_Is
   end // l_Is !
   if ( l_Is ! ) then
   begin
    anOurStereo .Inherited.Words 
    .trunc&gt; ( DROP l_Is ! ) 
    .for&gt; (
     TtfwWord IN anAncestor
     if ( anAncestor anOtherStereo call.me ) then
     begin
      true &gt;&gt;&gt; l_Is
     end
    ) 
   end // l_Is !
   l_Is
  ) &gt;&gt;&gt; Result
 ; // IsStereoKindOf

 TtfwWord VAR l_OurStereo
 Self .Stereotype &gt;&gt;&gt; l_OurStereo
 
 if ( l_OurStereo IsNil ) then
 begin
  false &gt;&gt;&gt; Result
 end // l_OurStereo IsNil
 else
 begin
  TtfwWord VAR l_Stereo
  aStereo DO &gt;&gt;&gt; l_Stereo
  l_OurStereo l_Stereo IsStereoKindOf &gt;&gt;&gt; Result
 end // // l_OurStereo IsNil
 
; // IsStereotype

BOOLEAN elem_func IsSummoned
 Self 'IsSummoned' false .ElemMember &gt;&gt;&gt; Result
; // IsSummoned

INTEGER elem_func Visibility
 Self 'Visibility' UnknownAccess /*PublicAccess*/ .ElemMember &gt;&gt;&gt; Result
; // Visibility

INTEGER elem_func LinkType
 Self 'LinkType' lt_unknown .ElemMember &gt;&gt;&gt; Result
; // LinkType

INTEGER elem_func Abstraction
 Self 'Abstraction' /*at_unknown*/ at_final .ElemMember &gt;&gt;&gt; Result
; // Abstraction

STRING elem_func GUID
 Self 'GUID' '' .ElemMember &gt;&gt;&gt; Result
; // GUID

STRING elem_func UID
 Self 'UID' '' .ElemMember &gt;&gt;&gt; Result
; // UID

INTEGER elem_func OpKind
 Self 'OpKind' opkind_Normal .ElemMember &gt;&gt;&gt; Result
; // OpKind

ModelElement elem_func Parent
 Self 'Parent' .ElemWord &gt;&gt;&gt; Result
; // Parent

ModelElement elem_func Implementor
 Self 'Implementor' .ElemWord &gt;&gt;&gt; Result
; // Implementor

USES
 LoadOnDemand.ms.dict
;

EXPORTS
 LoadOnDemand.ms.dict
 
USES
 MDProcess_CoreTemplates.tpi.script
;

EXPORTS
 MDProcess_CoreTemplates.tpi.script
 
USES
 MDProcess_Templates.tpi.script
;

EXPORTS
 MDProcess_Templates.tpi.script
 
USES
 MDProcess_ForDelphi.tpi.script
;

EXPORTS
 MDProcess_ForDelphi.tpi.script
 
USES
 MDProcess_ForF1.tpi.script
;

EXPORTS
 MDProcess_ForF1.tpi.script 
 
: st_in
 CONST Name 'in'
 @SELF
; // st_in

: st_inout
 CONST Name 'inout'
 @SELF
; // st_inout

: st_out
 CONST Name 'out'
 @SELF
; // st_out

: st_const
 CONST Name 'const'
 @SELF
; // st_const

: st_noconst
 CONST Name 'noconst'
 @SELF
; // st_noconst

: st_NodeType_
 CONST Name 'NodeType_'
 @SELF
; // st_NodeType_

: gen_rc.pas
 CONST UID '55B8D28B0157'
 CONST Name 'rc.pas'
 CONST Stereotype 'generator'
 @SELF
; // gen_rc.pas

: gen_res.cmd.pas
 CONST UID '55B8DD9C0301'
 CONST Name 'res.cmd.pas'
 CONST Stereotype 'generator'
 @SELF
; // gen_res.cmd.pas

: gen_pascal
 CONST UID '561D336E0099'
 CONST Name 'pascal'
 CONST Stereotype 'generator'
 @SELF
; // gen_pascal

: gen_forth_structure
 CONST UID '4F368B80039E'
 CONST Name 'forth_structure'
 CONST Stereotype 'generator'
 @SELF
; // gen_forth_structure