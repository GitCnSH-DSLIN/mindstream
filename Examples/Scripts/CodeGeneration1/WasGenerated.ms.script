PROGRAM WasGenerated.ms.script

USES
 axiom_push.ms.dict
 // - в этом словаре описано слово []
;

USES
 io.ms.dict
 // - в этом словаре определено слово Print
;

USES
 DogAndBracket.ms.dict
 // - в этом словаре описано слово @[
;

USES
 CompileTimeVar.ms.dict
 // - в этом словаре описано слово CompileTime-VAR
;

USES
 Testing.ms.dict
 // - в этом словаре описано слово Test&Dump
;

Test&Dump WasGenerated.ms.script.test

 %SUMMARY 'Метод WasGenerated.ms.script.test' ;

 ARRAY CompileTime-VAR g_WasGenerated []

 BOOLEAN FUNCTION .WasGenerated
   TtfwWord IN aWord
  %SUMMARY 'Определяет тот факт, что слово aWord уже использовалось для генерации' ;
  aWord g_WasGenerated array:Has ! ? (
   aWord array:AddTo g_WasGenerated
  ) // aValue l_Array array:Has ! ?
  false >>> Result
 ; // .WasGenerated

 : A
  %SUMMARY 'Метод A' ;
 ; // A

 : B
  %SUMMARY 'Метод B' ;
 ; // B

 : C
  %SUMMARY 'Метод C' ;
 ; // C

 @[ 
 // - слово @[ открывает массив ССЫЛОК, а не ЗНАЧЕНИЙ
  A A A B B B C C C A A A B B B C C C 
 ] 
 %REMARK ' - это массив ССЫЛОК на функции (слова)'
 .filter> ( .WasGenerated ! )
 %REMARK ' - отфильтровываем те слова, которые уже были использованы для генерации
  СПЕЦИАЛЬНО вызываем функцию .WasGenerated ДО всех ОСТАЛЬНЫХ фильтров'
 .filter> ( @ B != )
 %REMARK ' - отфильтровываем значения равные ссылке на B
  ВАЖНО: тут сравниваем УКАЗАТЕЛИ на функции'
 .map> pop:Word:Name 
 %REMARK ' - мапируем значение на вызов функции pop:Word:Name'
 .filter> ( 'C' != )
 %REMARK ' - отфильтровываем значения равные ''C''
  ВАЖНО: тут сравниваем ИМЕНА на функций'
 .for> 
 %REMARK ' - итерируем смапированные значения'
 .
 %REMARK ' - печатаем каждое итерируемое значение'

 g_WasGenerated Print
 %REMARK ' - печатаем слова участвовавшие в генерации.'
; // WasGenerated.ms.script.test

WasGenerated.ms.script.test
