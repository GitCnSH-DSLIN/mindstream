UNIT DumpStack.ms.dict

USES
 io.ms.dict
;

PROCEDURE DumpStackTo
  ^ IN aLambda
 %REMARK 'Вычисляемое значение'
  ^ IN aOutput
 %REMARK 'Устройство вывода'

 %SUMMARY 'Печатает состояние стека после вычисления выражения aLambda' ;

 INTEGER VAR l_StackLevel
 %REMARK 'Уровень стека'

 StackLevel >>> l_StackLevel

 aLambda DO
 %REMARK 'Вычисляем выражение aLambda. Оно может положить на стек "сколько угодно" значений'

 StackLevel l_StackLevel - >>> l_StackLevel
 %REMARK 'Вычисляем приращение стека'

 l_StackLevel >= 0 ?ASSURE 'Приращение стека не может быть отрицательным'

 while ( l_StackLevel > 0 )
 %REMARK 'Цикл по значениям в стеке'
 begin
  Dec l_StackLevel
  aOutput DO
  %REMARK 'Печатаем значение со стека'
 end // while ( l_StackLevel > 0 ) 
 
; // DumpStackTo

PROCEDURE DumpStack
  ^ IN aLambda
 %REMARK 'Вычисляемое значение'
 DumpStackTo ( aLambda DO ) Log
; // DumpStack