PROGRAM Proc.ms.script

USES
 CompileTimeVar.ms.dict
;

STRING CompileTime-VAR g_CurrentProc ''

MACRO proc
  Literal IN aName
  ^ IN aParams

 %SUMMARY 'Определяет процедуру с ПОЛНЫМ контролем типов' ;

 g_CurrentProc '' == ?ASSURE 'Вложенные процедуры пока не поддерживаются'

 aName |N >>> g_CurrentProc
 @SELF Ctx:SetWordProducerForCompiledClass
 axiom:PushSymbol VOID
 axiom:PushSymbol :
 g_CurrentProc Ctx:Parser:PushString

 aParams MembersIterator ==> ( 
  TtfwWord IN aParam 
  STRING VAR l_Stereo
  CONST cParamStereo 'IN'
  aParam pop:Word:Producer pop:Word:Name >>> l_Stereo
  l_Stereo cParamStereo == ?ASSURE [ 'Параметры ' @SELF pop:Word:Name ' должны быть помечены стереотипом ' cParamStereo ]
  l_Stereo Ctx:Parser:PushSymbol 
  aParam pop:Word:Name Ctx:Parser:PushSymbol  
 )

; // proc

MACRO proc-end
 %SUMMARY 'Завершает определение процедуры с ПОЛНЫМ контролем типов' ;

 axiom:PushSymbol ;

 axiom:PushSymbol VOID
 axiom:PushSymbol :
 '_' g_CurrentProc Cat Ctx:Parser:PushSymbol
 axiom:PushSymbol ;
 '' >>> g_CurrentProc
; // proc-end

USES
 Testing.ms.dict
;

Test&Dump ProcTest

proc A ( IN aValue )
 aValue .
proc-end // A

proc B ( STRING IN aValue )
 aValue .
proc-end // B

'Hello world' A
'Hello world' B

; // ProcTest

ProcTest
