// ElementsRTTI.ms.dict

USES
 Documentation.ms.dict
 EngineTypes.ms.dict
 axiom_push.ms.dict
 macro.ms.dict
 core.ms.dict
 NoStrangeSymbols.ms.dict
 params.ms.dict
 NoCapsLock.ms.dict
 arrays.ms.dict
;

ENGINE_WORD TYPE ModelElement
%REMARK 'Элемент модели'

PROCEDURE do_elem_func
 STRING IN aName
 ENGINE_WORD IN aSelf
 ENGINE_WORD IN aModifier
 STRING IN aType
 %SUMMARY 'Реализация elem_func, elem_proc и elem_generator' ;
 aSelf Ctx:SetWordProducerForCompiledClass
 axiom:PushSymbol :
 aName Ctx:Parser:PushLeftDottedSymbol
 aType Ctx:Parser:PushSymbol
 if ( aModifier =/= nil ) then
  ( aModifier .Name Ctx:Parser:PushSymbol )
 axiom:PushSymbol in
 'Self' Ctx:Parser:PushSymbol
; // do_elem_func

MACRO word_func
 Literal IN aName
 %SUMMARY 'Функция на элементе модели' ;
 aName .Name this.method.addr nil 'ENGINE_WORD' do_elem_func
; // word_func

MACRO elem_func
 Literal IN aName
 %SUMMARY 'Функция на элементе модели' ;
 aName .Name this.method.addr nil 'ModelElement' do_elem_func
; // elem_func

PROCEDURE do_elem_proc
 STRING IN aName
 ENGINE_WORD IN aSelf
 ENGINE_WORD IN aModifier
 %SUMMARY 'Реализация elem_proc и elem_generator' ;
 Ctx:ClearTypeInfo
 axiom:PushSymbol VOID
 aName aSelf aModifier 'ModelElement' do_elem_func
; // do_elem_proc

MACRO elem_proc
 Literal IN aName
 %SUMMARY 'Процедура на элементе модели' ;
 aName .Name this.method.addr nil do_elem_proc
; // elem_proc

MACRO elem_generator
 Literal IN aName
 %SUMMARY 'Генератор содержимого элемента' ;
 aName .Name this.method.addr nil do_elem_proc
; // elem_generator

MACRO elem_ref_proc
 Literal IN aName
 %SUMMARY 'Процедура на элементе модели, который передаётся по ссылке' ;
 aName .Name this.method.addr Addr LVALUE_MOD do_elem_proc
; // elem_ref_proc

ARRAY word_func ElementList 
 Literal IN aListName
 %SUMMARY 'Возвращает итератор именованного списка на элементе' ;

 private BOOLEAN FUNCTION FilterElement 
  OBJECT IN aMember

  Result := ( aMember NotValid ! )
 ; // FilterElement
 
 private OBJECT FUNCTION MapElement 
  OBJECT IN aMember

  VAR l_Element
  l_Element := ( aMember DO )
  // - берём ссылку на элемент модели
  if ( l_Element IsVoid ) then
  // - пропускаем невалидные ссылки
  (
   Result := nil
  )
  else
  (
   Result := l_Element
  ) 
 ; // MapElement

 if ( Self NotValid ) then
  ( Result := [ ] )
 else
  (
   VAR l_List
   l_List := ( Self %% ( aListName .Name ) )
   
   if ( l_List NotValid ) then
    ( Result := [ ] )
   else 
    ( Result := ( l_List CodeIterator .map> MapElement .filter> FilterElement ) )
  )
; // ElementList

ARRAY word_func Implemented.Words
 %SUMMARY 'Возвращает итератор элементов которые реализует наш элемент' ;
 Result := ( Self .ElementList %R )
; // Implemented

ARRAY word_func Inherited.Words
 %SUMMARY 'Возвращает итератор элементов от которого наследуется наш элемент' ;
 Result := ( Self .ElementList %G )
; // Inherited

BOOLEAN elem_func IsSummary
 %SUMMARY 
 '
  Определяет тот факт, что Self является документацией к элементу
 '
 ; // %SUMMARY
 Result := ( ( Self .Name ) = '%SUM' )
; // IsSummary

WordAlias .Stereotype %ST

BOOLEAN elem_func IsModelElement
 %SUMMARY 
 '
  Определяет тот факт, что Self является "элементом модели"
 '
 ; // %SUMMARY
 Result := (
  '<<' Self .Stereotype .Name StartsStr 
  AND
  ( '>>' Self .Stereotype .Name EndsStr )
 )
; // IsModelElement

ARRAY elem_func Implemented
 %SUMMARY 'Возвращает итератор элементов которые реализует наш элемент' ;
 Result := ( Self .Implemented.Words .filter> .IsModelElement )
; // Implemented

ARRAY elem_func Inherited
 %SUMMARY 'Возвращает итератор элементов от которого наследуется наш элемент' ;
 Result := ( Self .Inherited.Words .filter> .IsModelElement )
; // Inherited

ARRAY elem_func Children
 %SUMMARY 'Возвращает итератор детей Self в "терминах определённой модели"' ;
 Result := ( Self MembersIterator .filter> .IsModelElement )
; // Children

WordAlias .Parent %P

ARRAY elem_func Parents
 %SUMMARY 'Возвращает итератор родителей Self в "терминах определённой модели"' ;
 Result := ( ( Self LIST .Parent ) .filter> .IsModelElement )
; // Parents

ARRAY elem_func Stereotypes
 Result := ( Self LIST .Stereotype )
; // Stereotypes