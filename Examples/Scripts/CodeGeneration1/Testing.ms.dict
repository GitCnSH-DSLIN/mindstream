// Testing.ms.dict

USES
 axiom_push.ms.dict
 macro.ms.dict
 params.ms.dict
 io.ms.dict
 EngineTypes.ms.dict
 Documentation.ms.dict
 Sequence.It.ms.dict
;

CONST cTests 'Tests:'

MACRO TestsFor
 ENGINE_WORD RIGHT LINK IN aWord
  %REMARK 'aWord ссылка на слово справа от TestsFor'
 %SUMMARY 'Определяет набор тестов для слова aWord' ;

 axiom:PushSymbol VOID
 axiom:PushSymbol axiom:operator
 cTests aWord |N Cat Ctx:Parser:PushSymbol
; // TestsFor

PRIVATE PROCEDURE DoRunTestsFor
 STRING IN aTestedWordName
 ENGINE_WORD IN aTestsHolder
 %SUMMARY 'Выполняет тесты для aTestsHolder' ;

 [ 'Testing: ' aTestedWordName ] strings:Cat Print
 aTestsHolder MembersIterator ==> (
  IN aTest
   %REMARK 'aTest - вложенный элемент aTestsHolder'
  if ( ( aTest %ST |N ) = ( NameOf Test ) ) then
   %REMARK '- фильтруем только тесты.'
  begin
   aTest |N Print
    %REMARK 'Печатаем имя теста'
   aTest DO
    %REMARK 'Запускаем тест'
  end // ( ( aTest %ST |N ) = 'Test' )
 )
 [ 'Testing end: ' aTestedWordName ] strings:Cat Print
 '------------------' Print
; // DoRunTestsFor

PRIVATE PROCEDURE ExpandRunTestsFor
 ENGINE_WORD IN aTestsHolder
 STRING VAR l_Name
 aTestsHolder |N >>> l_Name
 STRING VAR l_TestsHolderName
 cTests l_Name Cat >>> l_TestsHolderName

 l_Name Ctx:Parser:PushString
 axiom:PushSymbol @
 l_TestsHolderName Ctx:Parser:PushSymbol
 axiom:PushSymbol DoRunTestsFor
; // ExpandRunTestsFor

PRIVATE PROCEDURE DoRunTests
 Sequence IN aWords
  %REMARK 'aWords ссылка на список слов слева от DoRunTests'
 %SUMMARY 'Выполняет тесты для списка слов aWords' ;
 aWords Sequence.It ==> ExpandRunTestsFor
; //DoRunTests

MACRO RunTests
 Sequence RIGHT LINK IN aWords
  %REMARK 'aWords ссылка на список слов справа от RunTests'
 %SUMMARY 'Выполняет тесты для списка слов aWords' ;
 aWords |^@ DoRunTests
; //RunTests
