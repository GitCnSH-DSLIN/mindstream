PROGRAM CallAncestor.ms.script

USES
 classRelations.ms.dict
;

USES
 WordsRTTI.ms.dict
;

USES
 Testing.ms.dict
;

Test&Dump CallAncestorTest

 TtfwWord FUNCTION .find.ancestor.method
   STRING IN aWordName
   TtfwWord IN aWhereToSearch

  nil &gt;&gt;&gt; Result

  aWhereToSearch .Inherited.Words .for&gt; ( 
   TtfwWord IN l_Ancestor

   TtfwWord VAR l_AncestorMember 
   l_Ancestor %% aWordName &gt;&gt;&gt; l_AncestorMember
   if ( l_AncestorMember IsNil ! ) then
   begin
    Result IsNil OR ( l_AncestorMember = Result ) 
     ?ASSURE 
      [ 'Method ' aWordName ' was already found in ' Result pop:Word:Parent pop:Word:Name ]
    l_AncestorMember &gt;&gt;&gt; Result
   end // l_AncestorMember IsNil !
  ) // aWhereToSearch .Inherited.Words .for&gt;

  if ( Result IsNil ) then
  begin
   aWhereToSearch .Inherited.Words .for&gt; ( 
    TtfwWord IN anAncestor
    TtfwWord VAR l_Found
    aWordName anAncestor call.me &gt;&gt;&gt; l_Found
    l_Found &gt;&gt;&gt; Result 
   ) // aWhereToSearch .Inherited.Words .for&gt;
  end // Result IsNil
 ; // .find.ancestor.method

 TtfwWord FUNCTION .find.method
   STRING IN aWordName
   TtfwWord IN aWhereToSearch
  aWhereToSearch %% aWordName &gt;&gt;&gt; Result

  if ( Result IsNil ) then
  begin
   aWordName aWhereToSearch .find.ancestor.method &gt;&gt;&gt; Result
  end // Result IsNil
 ; // .find.method

 : .call.method
   STRING IN aWordName
   TtfwWord IN aWhereToSearch
  TtfwWord VAR l_Method
  aWordName aWhereToSearch .find.method &gt;&gt;&gt; l_Method
  l_Method IsNil ?FAIL [ 'Method ' aWordName ' not found in word ' aWhereToSearch |N ]
  l_Method DO
 ; // .call.method

 : ?-&gt;
   ^@ IN aWhereToSearch
   Literal IN aWordName

  aWordName |N aWhereToSearch .call.method
 ; // ?-&gt;

 PROCEDURE .call.ancestor.do
  STRING IN aWordName
  TtfwWord IN aWord

  TtfwWord VAR l_WordParent
  aWord pop:Word:Parent &gt;&gt;&gt; l_WordParent
  l_WordParent IsNil ! ?ASSURE [ 'No parent for ' aWordName ]

  TtfwWord VAR l_FoundWord
  aWordName l_WordParent .find.ancestor.method &gt;&gt;&gt; l_FoundWord

  l_FoundWord IsNil ! ?ASSURE [ 'Method ' aWordName ' not found in ' l_WordParent pop:Word:Name ]
  l_FoundWord CompileValue
 ; // .call.ancestor.do

 MACRO .call.ancestor
  STRING VAR l_WordName
  Ctx:KeyWordDefiningNow pop:KeyWord:Name &gt;&gt;&gt; l_WordName
  l_WordName IsNil ! ?ASSURE [ 'No name for word calling ' @SELF pop:Word:Name ]

  l_WordName Ctx:WordDefiningNow .call.ancestor.do
 ; // .call.ancestor

 : A
  : X
    INTEGER IN aValue
   aValue 1 +
  ; // X
 ; // A

 : B Inherits A
  : X
    INTEGER IN aValue
   aValue 2 + .call.ancestor
  ; // X
 ; // B

 : C Inherits B
 ; // C

 : D Inherits C
  : X
    INTEGER IN aValue
   aValue 4 + .call.ancestor
  ; // X
 ; // D

 : E Inherits C
  : X
    INTEGER IN aValue
   aValue 5 + .call.ancestor
  ; // X
 ; // E

 1 A ?-&gt; X .
 2 B ?-&gt; X .
 3 C ?-&gt; X .
 4 D ?-&gt; X .
 5 E ?-&gt; X .

; // CallAncestorTest

CallAncestorTest
