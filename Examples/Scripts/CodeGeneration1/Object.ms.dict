UNIT Object.ms.dict

USES
 Bind.ms.dict
;

EXPORTS
 Bind.ms.dict

USES
 core.ms.dict
 macro.ms.dict
 NoCapsLock.ms.dict
 implementation.ms.dict
 params.ms.dict
 axiom_push.ms.dict
 Documentation.ms.dict
 WordsRTTI.ms.dict
 arrays.ms.dict
 io.ms.dict
 Debug.ms.dict
 CompileTimeVar.ms.dict
;

EXPORTS
 CompileTimeVar.ms.dict

EXPORTS
 implementation.ms.dict

USES
 InheritsAndImplementsNew.ms.dict
;

EXPORTS
 InheritsAndImplementsNew.ms.dict

// Понятное дело, что всю обвязку потом упрячем в отдельный словарь object.ms.dict

NamedWordProducer %PROPERTIES %Props
NamedWordProducer %METHODS %Methods
NamedWordProducer %CLASS-METHODS %Class-Methods
NamedWordProducer %CONSTRUCTORS %Constructors

OBJECT FUNCTION DoMember 
 OBJECT IN aMember
 aMember DO >>> Result
; // DoMember

BOOLEAN FUNCTION FilterMember 
 OBJECT IN aMember
 Result := ( aMember NotValid ! )
; // FilterMember
 
WordAlias %R .Implemented.Words
WordAlias %G .Inherited.Words

ARRAY FUNCTION %ClassRTTIList
 IN %S
 Literal IN aName
 VAR l_List
 %S %% ( aName |N ) >>> l_List
 if ( l_List NotValid ) then
  ( Result := [ ] )
 else 
  ( Result := ( l_List CodeIterator ) )
; // %ClassRTTIList

ARRAY FUNCTION %Props 
 IN %S
 %SUMMARY 'Возвращает итератор свойств класса' ;
 %S %ClassRTTIList %Props >>> Result
; // %Props

ARRAY FUNCTION %Methods 
 IN %S
 %SUMMARY 'Возвращает итератор методов класса' ;
 %S %ClassRTTIList %Methods >>> Result
; // %Methods

ARRAY FUNCTION %Class-Methods 
 IN %S
 %SUMMARY 'Возвращает итератор методов мета-класса' ;
 %S %ClassRTTIList %Class-Methods >>> Result
; // %Class-Methods

ARRAY FUNCTION %Constructors 
 IN %S
 %SUMMARY 'Возвращает итератор конструкторов класса' ;
 %S %ClassRTTIList %Constructors >>> Result
; // %Constructors

MACRO class_impl
  Literal IN aName
 %SUMMARY 'Реализация класса.
 Тут мы будем хранить всю информацию о классе - предки, поля, методы'
 ;

 @SELF Ctx:SetWordProducerForCompiledClass
 axiom:PushSymbol : 
 aName |N Ctx:Parser:PushSymbol
; // class_impl

STRING CompileTime-VAR g_CurrentClass ''
STRING CompileTime-VAR g_CurrentClassImpl ''
BOOLEAN CompileTime-VAR g_InstanceSizeDefined false

TtfwWord type ObjectClass

ObjectClass CompileTime-VAR g_CurrentClassImplWord nil

CONST cFieldPrefix 'c:Offset:'

PRIVATE STRING operator MakeFieldOffsetName
  STRING IN aName

 %SUMMARY 'Делает имя для доступа к полю' ; 
 [ cFieldPrefix 
   aName 
 ] strings:Cat >>> Result
; // MakeFieldOffsetName

INTEGER type FieldOffset
// - смещение поля

VOID operator define_member
 STRING IN aName
 FieldOffset IN aOffset

 axiom:PushSymbol implementation
 g_CurrentClassImplWord pop:Word:Name Ctx:Parser:PushSymbol
 axiom:PushSymbol Const
 aName MakeFieldOffsetName Ctx:Parser:PushSymbol
 aOffset Ctx:Parser:PushInt
 axiom:PushSymbol end.

; // define_member

FieldOffset CompileTime-VAR g_ClassFieldOffset 0

ARRAY CompileTime-VAR g_CurrentClassMembers [ ]

ARRAY CompileTime-VAR g_CurrentClassProperties [ ]

ARRAY CompileTime-VAR g_CurrentClassMethods [ ]

ARRAY CompileTime-VAR g_CurrentMetaClassMethods [ ]

ARRAY CompileTime-VAR g_CurrentClassConstructors [ ]

MACRO member
  Literal IN aName
  STRING VAR l_Name

 %SUMMARY 'Определяет член текущего класса' ;
 g_InstanceSizeDefined ?FAIL [ 'Класс ' g_CurrentClass ' уже определён. Нельзя доопределять его члены.' ]
 aName |N >>> l_Name
 Ctx:ClearTypeInfo
 l_Name g_ClassFieldOffset define_member
 Inc g_ClassFieldOffset
 l_Name array:AddTo g_CurrentClassMembers
; // member

PRIVATE STRING operator MakeMethodSignaturePrim
 STRING IN aClass
 STRING IN aName
 [ aClass ':' aName ] strings:Cat >>> Result
; // MakeMethodSignaturePrim

CONST cClassImplPrefix '_:'

STRING FUNCTION ClassImplName
  STRING IN aName
 [ cClassImplPrefix aName ] strings:Cat >>> Result
; // ClassImplName

Const ClassRefSuffix 'Class'

STRING FUNCTION ClassRefName
  STRING IN aName
 [ aName ClassRefSuffix ] strings:Cat >>> Result
; // ClassRefName

CONST cSelf 'Self'

STRING CompileTime-VAR g_CurrentClassMethod ''
STRING CompileTime-VAR g_CurrentClassMethodModifiers ''

PRIVATE VOID operator MakeMethodSignature
 STRING IN aName
 STRING VAR l_Signature
 g_CurrentClass aName MakeMethodSignaturePrim >>> l_Signature
 if ( l_Signature IsWordDeclared ) then
 begin
  axiom:PushSymbol REDEFINITION
 end
 axiom:PushSymbol :
 l_Signature Ctx:Parser:PushSymbol
; // MakeMethodSignature

PRIVATE VOID operator MakeSelfParam
 g_CurrentClass Ctx:Parser:PushSymbol
 axiom:PushSymbol in
 cSelf Ctx:Parser:PushSymbol
; // MakeSelfParam

MACRO method
  Literal IN aName

 %SUMMARY 'Метод объекта' ;

 STRING VAR l_Name
 aName |N >>> l_Name

 l_Name @SELF bindInPlace
 
 //@SELF Ctx:SetWordProducerForCompiledClass

 l_Name MakeMethodSignature
 MakeSelfParam
 l_Name array:AddTo g_CurrentClassMethods
; // method

MACRO class-method
  Literal IN aName
  
 %SUMMARY 'Метод класса' ;

 g_CurrentClassMethod '' == ?ASSURE 'Вложенные методы класса пока не поддерживаются'

 TtfwWordInfo VAR l_WordInfo
 Ctx:WordInfo >>> l_WordInfo

 STRING VAR l_TypeInfo
 l_WordInfo pop:WordInfo:TypeName >>> g_CurrentClassMethodModifiers

 STRING VAR l_Name
 aName |N >>> l_Name

 l_Name >>> g_CurrentClassMethod

 l_Name bindAndRestoreContext

 @SELF Ctx:SetWordProducerForCompiledClass

 axiom:PushSymbol :

 STRING VAR l_ClassName

 g_CurrentClass >>> l_ClassName
 
 l_ClassName ClassRefName >>> l_ClassName

 STRING VAR l_ClassMethodName
 
 l_ClassName l_Name MakeMethodSignaturePrim >>> l_ClassMethodName
 l_ClassMethodName Ctx:Parser:PushSymbol

 l_ClassName Ctx:Parser:PushSymbol
 axiom:PushSymbol in
 cSelf Ctx:Parser:PushSymbol

 l_Name array:AddTo g_CurrentMetaClassMethods
; // class-method

CONST cObjectName 'Object'

CONST cInstanceSizeName 'Instance:Size'

STRING FUNCTION InstanceSizeName
 [ cFieldPrefix cInstanceSizeName ] strings:Cat >>> Result
; // InstanceSizeName

//FORWARD classExpander
// - так почему-то не работает, хотя должно бы

CONST cClassTypePrefix 'ClassOf::'

STRING FUNCTION ClassOfName
  STRING IN aClassName
 [ cClassTypePrefix aClassName ] strings:Cat >>> Result
; // ClassOfName

MACRO ClassOf
  Literal IN aName
 STRING VAR l_ClassName
 aName |N ClassOfName >>> l_ClassName
 l_ClassName IsWordDeclared ?ASSURE [ 'Класс ' l_ClassName ' не определён' ]
 l_ClassName Ctx:Parser:PushSymbol
; // ClassOf

MACRO class
 Literal IN aName
 ( g_CurrentClass = '' ) ?ASSURE 'Вложенные классы пока не поддерживаются'
 g_CurrentClassMembers := [ ]
 g_CurrentClassProperties := [ ]
 g_CurrentClassMethods := [ ]
 g_CurrentMetaClassMethods := [ ]
 g_CurrentClassConstructors := [ ]
 g_ClassFieldOffset := 0
 g_InstanceSizeDefined := false
 aName |N >>> g_CurrentClass
 STRING VAR l_CurrentClassImpl
 g_CurrentClass ClassImplName >>> l_CurrentClassImpl

 l_CurrentClassImpl >>> g_CurrentClassImpl

 STRING VAR l_ClassRefName
 g_CurrentClass ClassRefName >>> l_ClassRefName

 if ( g_CurrentClass !== cObjectName ) then
 begin
  axiom:PushSymbol ObjectClass
  axiom:PushSymbol type
  l_ClassRefName Ctx:Parser:PushSymbol
 end // g_CurrentClass !== cObjectName

 l_ClassRefName Ctx:Parser:PushSymbol
 axiom:PushSymbol class_impl
 l_CurrentClassImpl Ctx:Parser:PushSymbol

 [
   if ( g_CurrentClass !== cObjectName ) then
   begin
    '%INHERITS'
    '@' 
    cObjectName ClassImplName
    ';'
   end

  '@SELF'
  '>>>'
  'Result'

  'FieldOffset'
  'CompileTime-VAR'
  InstanceSizeName
 ] Ctx:Parser:PushArray

  0 Ctx:Parser:PushInt

 axiom:PushSymbol ;

 [
  '[EXECUTE] ('
  l_CurrentClassImpl
  '>>>'
  'g_CurrentClassImplWord'
  ')'
  
  'array' 
  'type' 
  g_CurrentClass

  'WordAlias'
  g_CurrentClass ClassOfName
  l_CurrentClassImpl

 ] Ctx:Parser:PushArray

 if ( g_CurrentClass !== cObjectName ) then
 begin
  'classExpander' Ctx:Parser:PushSymbol
  l_CurrentClassImpl Ctx:Parser:PushSymbol
 end

; // class

class Object

FieldOffset member VMT

MACRO readonly
  Literal IN aName

 %SUMMARY 'read-only свойство объекта' ;

 STRING VAR l_Name
 aName |N >>> l_Name

 l_Name @SELF bindInPlace

 //@SELF Ctx:SetWordProducerForCompiledClass

 l_Name MakeMethodSignature
 MakeSelfParam
 l_Name array:AddTo g_CurrentClassProperties
; // readonly

: FieldByOffset
 Object in Self
 FieldOffset right anOffset
 anOffset Self [i]
; // FieldByOffset

PRIVATE operator do-get-member
  STRING IN aName
 
 %SUMMARY 'Определяет способ доступа к члену класса' ;
 axiom:PushSymbol FieldByOffset
 axiom:PushSymbol (
 g_CurrentClass ClassImplName Ctx:Parser:PushSymbol
 axiom:PushSymbol ::
 aName MakeFieldOffsetName Ctx:Parser:PushSymbol 
 axiom:PushSymbol )
; // do-get-member

CONST cResult 'Result'

MACRO read
 Literal IN aName
 cSelf Ctx:Parser:PushSymbol 
 aName |N do-get-member
 axiom:PushSymbol >>>
 cResult Ctx:Parser:PushSymbol
 axiom:PushSymbol ;
; // read

ObjectClass readonly class read VMT

MACRO class-method-end
 axiom:PushSymbol ;
 g_CurrentClassMethodModifiers axiom:PushWordInfo
 axiom:PushSymbol method
 g_CurrentClassMethod Ctx:Parser:PushSymbol
 'Self' Ctx:Parser:PushSymbol 
 '.class' Ctx:Parser:PushSymbol 
 [ '.' g_CurrentClassMethod ] strings:Cat Ctx:Parser:PushSymbol

 if ( g_CurrentClassMethodModifiers IsNil ! ) then
 begin 
  axiom:PushSymbol >>> 
  cResult Ctx:Parser:PushSymbol
 end // g_CurrentClassMethodModifiers IsNil !

 axiom:PushSymbol ;
 '' >>> g_CurrentClassMethod
 '' >>> g_CurrentClassMethodModifiers
; // class-method-end

STRING class-method ClassName
 Self |N ':' string:Split >>> Result DROP
class-method-end // ClassName

MACRO VMTFieldOffset
  ^@ IN aClass
  Literal IN aFieldName

 ObjectClass VAR l_Class
 aClass |^ >>> l_Class

 STRING VAR l_ClassName
 l_Class .ClassName >>> l_ClassName

 STRING VAR l_FieldName
 aFieldName |N >>> l_FieldName
 l_FieldName MakeFieldOffsetName >>> l_FieldName

 axiom:PushSymbol ClassOf
 l_ClassName Ctx:Parser:PushSymbol

 axiom:PushSymbol ::
 l_FieldName Ctx:Parser:PushSymbol
 
; // VMTFieldOffset

INTEGER class-method InstanceSize
 Self %% InstanceSizeName DO >>> Result
class-method-end // InstanceSize

MACRO classExpander
  ^ IN anImpl

 %SUMMARY 'Тут можно копировать поля и методы' ;
 anImpl |@ %G .for> ( 
  ObjectClass IN anItem 

  INTEGER VAR l_ClassFieldOffset
  l_ClassFieldOffset := 0

  anItem MembersIterator 
   .filter> ( pop:Word:Producer pop:Word:Name NameOf CONST == )
   .filter> ( pop:Word:Name cFieldPrefix SWAP StartsStr )  
   .for> 
  ( 
   TtfwWord IN aMember
   axiom:PushSymbol member

   aMember DO >>> g_ClassFieldOffset

   if ( l_ClassFieldOffset < g_ClassFieldOffset ) then
   // - тут исправляем "кривизну" с сортировкой
   begin
    g_ClassFieldOffset >>> l_ClassFieldOffset
   end

   [ aMember pop:Word:Name [ cFieldPrefix ] strings:Cat '' string:ReplaceFirst ] strings:Cat Ctx:Parser:PushSymbol
   
  )

  l_ClassFieldOffset >>> g_ClassFieldOffset
  //Inc g_ClassFieldOffset
  // - это тут не надо

  anItem %Props .for> ( 
   IN aProp
   axiom:PushSymbol WordAlias
   g_CurrentClass aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
   anItem .ClassName aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
  ) // anItem %Props .for>

  anItem %Methods .for> ( 
   IN aProp
   axiom:PushSymbol WordAlias
   g_CurrentClass aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
   anItem .ClassName aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
  ) // anItem %Methods .for>

  anItem %Class-Methods .for> ( 
   IN aProp

   axiom:PushSymbol WordAlias
   g_CurrentClass ClassRefName aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
   anItem .ClassName ClassRefName aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
  )

  anItem %Constructors .for> ( 
   IN aProp
   axiom:PushSymbol WordAlias
   g_CurrentClass aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
   anItem .ClassName aProp |N MakeMethodSignaturePrim Ctx:Parser:PushSymbol
  ) // anItem %Constructors .for>

 )
; // classExpander

: ListToNameAsString
 STRING IN aName
 ARRAY IN aList
 aName
  aList .for> NameAsString
 ';'
; // ListToNameAsString

MACRO RunCompileProps&Methods
 [
  'implementation'
  g_CurrentClassImpl

   NameOf %PROPERTIES g_CurrentClassProperties ListToNameAsString

   NameOf %METHODS g_CurrentClassMethods ListToNameAsString

   NameOf %CLASS-METHODS g_CurrentMetaClassMethods ListToNameAsString

   NameOf %CONSTRUCTORS g_CurrentClassConstructors ListToNameAsString

  'end.'
 ] Ctx:Parser:PushArray
; // RunCompileProps&Methods

PROCEDURE DefineInstanceSize
 %SUMMARY 'Определяем размер экземпляров класса' ;
 if ( g_InstanceSizeDefined ! ) then
 begin
  g_InstanceSizeDefined := true
  g_CurrentClassImplWord %% InstanceSizeName DO =0 ?ASSURE [ 'Размер класса ' g_CurrentClass ' почему-то уже определён' ]
  g_ClassFieldOffset g_CurrentClassImplWord %% InstanceSizeName pop:Word:SetValue
 end // g_InstanceSizeDefined !
; // DefineInstanceSize

MACRO class-end
 axiom:PushSymbol RunCompileProps&Methods

 DefineInstanceSize

 g_CurrentClass := ''
 g_InstanceSizeDefined := false
; // class-end

MACRO constructor
  Literal IN aName
  ^ IN aParams

 %SUMMARY 'Конструктор объектов';

 DefineInstanceSize
 // - тут определяем размер экземпляров класса

 STRING VAR l_Name
 aName |N >>> l_Name
 @SELF Ctx:SetWordProducerForCompiledClass
 g_CurrentClass Ctx:Parser:PushSymbol
 l_Name MakeMethodSignature
 @SELF NameOf right aParams axiom:Params:PushWithOtherStereo

 axiom:PushSymbol WordAlias
 cSelf Ctx:Parser:PushSymbol
 cResult Ctx:Parser:PushSymbol

 l_Name array:AddTo g_CurrentClassConstructors
; // constructor

MACRO new[
 ClassOf Object .InstanceSize Msg
 g_CurrentClassImplWord %% InstanceSizeName DO .
 axiom:PushSymbol [
 axiom:PushSymbol @
 g_CurrentClassImplWord pop:Word:Name Ctx:Parser:PushSymbol
; // new[

MACRO get-member
  Literal IN aName

 %SUMMARY 'Компилирует способ доступа к члену класса' ;
 aName |N do-get-member
; // get-member

constructor new ( )
 new[ ] >>> Result
; // new

VIRTUAL STRING method ToPrintable
 //Self ToPrintable >>> Result
 Self .ClassName >>> Result
; // ToPrintable

VIRTUAL void method Print
 Self .ToPrintable Print
; // Print

class-end // Object
